/**
 * File:    facade_mix.cga
 * Created: 10 Sep 2015 14:09:44 GMT
 * Author:  stefalie
 */
 
version "2015.2"

const WHITE_TEX = "assets/white.png"
const WHITE_COLOR = "#ffffff"


// facade dimensions
attr groundFloor_height	= 6
attr floor_height 			= 4
attr lowerGround_height	= 1.2					// lower part of ground floor (darker bricks)
attr upperGround_height	= 6 - 0.7 - 1.2			// upper part of ground floor (lighter bricks)

// facade element sizes
attr base_height			= 0.7					// base around bottom of entire building
attr base_depth			= 0.1
attr door_width			= 2.6
attr door_height			= 4.4
attr door_depth			= 1.8
attr ledge_height			= 0.3
attr ledge_depth			= 0.2
attr window_width			= 1.7
attr window_height 		= 2.6
attr window_depth			= 0.3
attr wallMargin_width		= 1.2
attr pillar_width			= 0.4
attr pillar_depth			= 0.1

// colors
attr wall_color		= "#EED6A2"					// yellow wall of upper floors
attr brick_color		= "#929280"					// brick color of upper part of ground floor (lighter gray)
attr door_color		= "#4F2B21"
attr base_color		= brick_color
attr ledge_color		= brick_color
attr window_color		= brick_color
attr pillar_color		= brick_color

// brick texture
const brickTex_width	= 3.25
const brickTex_height	= 1.75


// ------------------------------
// Assets
// ------------------------------

// assets
cylinder_asset 			= "/SimpleSternwarte/assets/cylinder.obj"
dome_asset				= "/SimpleSternwarte/assets/hemisphere.obj"
base_asset				= "/SimpleSternwarte/assets/base.obj"
ledge_asset				= "/SimpleSternwarte/assets/midLedge.obj"
pillar_asset			= "/SimpleSternwarte/assets/pillar_middle.obj"
windowFrame_asset		= "/SimpleSternwarte/assets/window_frame.obj"
windowGlass_asset		= "/SimpleSternwarte/assets/window_glass.obj"
windowWall_asset		= "/SimpleSternwarte/assets/window_wall.obj"
doorFrame_asset			= "/SimpleSternwarte/assets/door_frame.obj"
doorWall_asset			= "/SimpleSternwarte/assets/door_wall.obj"
doorPlane_asset			= "/SimpleSternwarte/assets/door_plane.obj"

// textures
brickWall_tex			= "/SimpleSternwarte/assets/brickWall.jpg"
door_tex				= "/SimpleSternwarte/assets/door.jpg"


@StartRule
FrontFacade -->
	//texture(WHITE_TEX)
	color(wall_color)
	alignScopeToGeometry(zUp, 0, world.lowest)
	split(y) { groundFloor_height : GroundFloorDoor
			 | {~floor_height	  : Floor}* }
			 
GroundFloorWindows -->
	split(y) { lowerGround_height : Wall
			 | window_height 	  : split(x) { ~wallMargin_width : Wall
			 								 | { window_width      : Window
			   								   | ~wallMargin_width : Wall }*
			   								 }
			 | ~1 			 	  : Wall }
			 
// ground floor with door
GroundFloorDoor -->
	color(brick_color)
	texture(brickWall_tex)
	setupProjection(0, scope.xy, brickTex_width, brickTex_height)
	split(y) { base_height	: Base
			 | door_height	: split(x) { ~1			: GroundFloorWindows
			 						   | door_width : Door
			 						   | ~1			: GroundFloorWindows }
			 | ~1 		  	: Wall
			 | ledge_height : Ledge }
			 		
// upper floor	 
Floor -->
	split(y) { ~1			: split(x) { pillar_width  	  : Pillar
			 						   | wallMargin_width : Wall
			 						   | ~1				  : split(x) { pillar_width : Pillar
			 	 						   							 | ~1 			: Wall
			 	 						   							 | window_width : split(y) { window_height : Window
			 	 						   							   						   | ~1 		   : Wall }
			 	 						   							 | ~1 			: Wall }*
								 	   | pillar_width	  : Pillar
			 						   | wallMargin_width : Wall
			 						   | pillar_width 	  : Pillar }
			 | ledge_height : Ledge }

Base -->
	s(scope.sx + 2*base_depth, '1, base_depth)			// extend in x past facade, will get trimmed
	center(x)
	i(base_asset)
	deleteUV(0)
	color(base_color)
		 

Ledge -->
	s(scope.sx + 2* ledge_depth , '1, ledge_depth )		// extend in x past facade, will get trimmed
	center(x)
	color(ledge_color)
	i( ledge_asset )
	deleteUV(0)
	
Door -->
	s('1, '1, door_depth)
	DoorWall
	t(0, 0, -door_depth*0.95)
	DoorFrame
	DoorPlane
	
DoorFrame -->	
	i(doorFrame_asset)
	color(brick_color)
	deleteUV(0)
	
DoorPlane -->
	i(doorPlane_asset)
	texture(WHITE_TEX)
	deleteUV(0)
	color(wall_color)
	split(x) { ~1   : Wall
			 | '0.7 : split(y) { '0.75 : DoorTextureArea
			 				   | ~1    : Wall }
			 | ~1   : Wall }
			 
DoorTextureArea -->
	setupProjection(0, scope.xy, '1, '1)
	color(WHITE_COLOR)
	texture(door_tex)
	projectUV(0)


//door_assetHeight = assetInfo(doorWall_asset, ty) - assetInfo(doorFrame_asset, ty) + assetInfo(doorWall_asset, sy)
//doorWall_relSize = assetInfo(doorWall_asset, sy)/door_assetHeight
const doorWall_relSize = 0.29379887
DoorWall -->
	split(y) { ~1 						 : NIL
			 | scope.sy*doorWall_relSize : i(doorWall_asset)
			 							   Wall
			 }

//windowWall_relSize = assetInfo(windowWall_asset, sy)/assetInfo(windowFrame_asset, sy)
const windowWall_relSize = 0.29196

Window -->
	s('1, '1, window_depth)
	WindowFrame
	WindowGlass
	[ split(y) { ~1 						   : NIL
		 	   | scope.sy*windowWall_relSize : i(windowWall_asset) Wall } ]

	
// frame and inner grid of window
WindowFrame -->
	t(0, 0, '-0.8)
	i(windowFrame_asset)
	color(brick_color)
	deleteUV(0)
	
// window glass
WindowGlass -->
	t(0, 0, '-0.15)
	i(windowGlass_asset)
	deleteUV(0)
    color(0.2,0.2,0.25)
    set(material.ambient.r,0) set(material.ambient.g,0) set(material.ambient.b,0.1)
    set(material.specular.r,0.8) set(material.specular.g,0.8) set(material.specular.b,0.8)
    set(material.reflectivity,0.6) set(material.shininess,50) 
	
Pillar -->
	s('1, '1, pillar_depth)
	i( pillar_asset )
	deleteUV(0)
	color(pillar_color)
	
Wall -->
	projectUV(0)
